apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

kapt {
    arguments {
        arg("AROUTER_MODULE_NAME", project.name)
    }
}

android {
    compileSdkVersion build_versions.SDK_COMPILE

    defaultConfig {
        minSdkVersion build_versions.SDK_MIN
        targetSdkVersion build_versions.SDK_TARGET

        versionCode 1
        versionName "1.0.0"

        // apk 方法数超过 65536 限制，进行分包处理
        multiDexEnabled true
        dexOptions {
            javaMaxHeapSize "3g"
        }
    }

    buildTypes {
        release {
            // 开启混淆
            minifyEnabled true
            // 压缩优化
            zipAlignEnabled true
            // 默认的混淆文件以及我们指定的混淆文件，子模块使用 consumerProguardFiles
            consumerProguardFiles 'proguard-rules.pro'
        }
        debug {
            // 开启混淆
            minifyEnabled false
            // 压缩优化
            zipAlignEnabled false
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }

    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])
    api deps.kotlin.stdlib
    kapt deps.arouter_compiler

    /*网络服务*/
    if ('bintray.pull.lib'.toBoolean()) {
        api 'vip.qsos:netservice:1.0.0'
    } else {
        api project(":lib:netservice")
    }

}


// 以下为发布maven包配置，开发可删除
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

version = "1.0.0"
group = "vip.qsos"

def siteUrl = 'https://bintray.com/vip-qsos/abcl'
def gitUrl = 'https://github.com/hslooooooool/abcl.git'
def libName = "core-lib"

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name 'core-lib'
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'vip-qsos'
                        name 'hua qing song'
                        email 'hslooooooool@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

artifacts {
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('mavenAccount.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")

    configurations = ['archives']

    pkg {
        repo = 'maven'
        name = libName
        licenses = ['Apache-2.0']
        websiteUrl = siteUrl
        vcsUrl = gitUrl

        publish = true
    }
}